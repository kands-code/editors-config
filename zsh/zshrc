# for zsh
fpath=(
  /opt/homebrew/share/zsh/site-functions
  ~/.config/zsh/zfunc
  "${fpath[@]}")
autoload -U +X compinit && compinit
autoload -U +X bashcompinit && bashcompinit
zstyle ':completion:*' use-cache on
zstyle ':completion:*' menu select
zstyle ':completion:*' completer _complete _match _approximate
zstyle ':completion:*:approximate:*' max-errors 1 numeric
zstyle ':completion:*' cache-path "$HOME/.cache/zsh/.compcache"
autoload -Uz promptinit
promptinit
prompt walters

# utils config
export GPG_TTY=$(tty)
export GNUPGHOME="$HOME/.local/share/gnupg"
export PATH="$paths[homebrew]":"$PATH" # homebrew first

# daily update
dailyup() {
  # brew
  brew update
  brew upgrade
  brew autoremove
  brew cleanup --prune=all
  # rust
  if command -v rustup >/dev/null 2>&1; then
    rustup update
    if [ "$?" = '0' ]; then
      cargo install --all-features \
        cargo-generate \
        create-tauri-app \
        mdbook \
        mdbook-katex \
        mdbook-toc \
        tauri-cli
    fi
  fi
}

# reload dock
if command -v defaults >/dev/null 2>&1; then
  reload-dock() {
    defaults write com.apple.dock ResetLaunchPad -bool true && killall Dock
  }
fi

#? opam
if command -v opam >/dev/null 2>&1; then
  export OPAM_DEV_SWITCH="dev"
  eval "$(opam env)"
  activate-switch() {
    if [[ "$#" -eq "1" ]]; then
      if opam switch list | grep "$1" >/dev/null 2>&1; then
        opam switch set $1
      else
        echo "=> swithc ($1) not found"
      fi
    else
      echo "usage: activate-switch [switch name]"
    fi
  }
  create-switch() {
    if [[ "$#" -le "1" ]]; then
      local switchname="$(basename $PWD)"
      local switchfile="switch-state.txt"
      if [[ -n "$1" ]]; then
        switchname="$1"
      fi
      if opam switch list | grep "$switchname" >/dev/null 2>&1; then
        opam switch set "$switchname"
      else
        opam switch export "$switchfile"
        opam switch create "$switchname" --empty
        opam switch import "$switchfile"
        opam switch set "$switchname"
        eval $(opam env)
        if [[ -f "$switchfile" ]]; then
          rm "$switchfile"
        fi
      fi
    else
      echo "usage: create-switch [switch name]"
      return 1
    fi
  }
  # defaultly activate dev switch
  activate-switch $OPAM_DEV_SWITCH
fi
#? rye
if [[ -n "$RYE_HOME" ]] && [[ -f "$RYE_HOME/env" ]]; then
  source "$RYE_HOME/env"
fi

# python venv
if command -v python3 >/dev/null 2>&1; then
  create-venv() {
    local vname="$(basename $PWD)"
    if [[ -n "$1" ]]; then
      vname="$1"
    fi
    python3 -m venv .venv --prompt "$vname"
  }
  activate-venv() {
    local dname=".venv"
    if [[ -n "$1" ]]; then
      dname="$1"
      if [[ -d "$PWD/$dname" ]] && [[ -f "$PWD/$dname/bin/activate" ]]; then
        source "$PWD/$dname/bin/activate"
      fi
      return 0
    else
      echo "usage: activate-venv ([venv folder])"
      return 1
    fi
  }
fi

# video convert
if command -v ffmpeg >/dev/null 2>&1; then
  # convert to mp4
  conv2mp4() {
    local filename="$1"
    local basename="${filename%.*}"
    ffmpeg -i "$filename" \
      -vf "pad=ceil(iw/2)*2:ceil(ih/2)*2" \
      -y -an -crf 0 \
      "$basename".mp4
  }
  # aria2c wrapper
  a2c() {
    if command -v aria2c >/dev/null 2>&1; then
      aria2c --max-connection-per-server=4 \
        --min-split-size=5M \
        --continue \
        "$@"
    fi
  }
  # yt-dlp wrapper
  yd() {
    if command -v yt-dlp >/dev/null 2>&1; then
      local url="$1"
      shift
      if [ -z "$url" ]; then
        echo "usage: yd [url] (...[yt-dlp arguments])"
        return 1
      fi
      yt-dlp -o "%(uploader_id)s/%(playlist_title)s/p%(playlist_index)03d-%(title)s.%(ext)s" \
        -f "bestvideo+bestaudio" \
        --recode-video="mp4" \
        --write-auto-subs \
        --sub-format="vtt" \
        --sub-langs="en.*,zh-Hans" \
        --embed-subs \
        $url \
        "$@"
      return 0
    fi
  }
fi
